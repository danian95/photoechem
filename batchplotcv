#!/bin/zsh

# Init default variables
# area=0.9694 # cm^2
# area=0.064516 # Tianyue's devices
tmpfile=/tmp/batchplot.tmp

read "area?Sample area (cm^2)? [0.025]: "
area=${area:-0.025}

printf "Using %s cm^2 as electrode area.\n\n" "$area" | tee $tmpfile
printf "Sample\tEpoch_Time/s\tDirection\tj_SC/mA.cm^-2\tV_OC/V\tP_t/mW.cm^-2\tP_max/mW.cm^-2\tFF\tPCE\n" | tee -a $tmpfile

for light in *1\ sun*.txt; do
	printf "Data: %s\n" "${light%,\	1\ sun*}"
	_sample=${${light}%%.*}
	_carbon=${${${light}#*.}%%,*}
	_comments=${${${light}#*, }%,*.txt}
	if [[ $light == *"run"* ]]; then
		_run=.light_run${${${light}/*run /}/,*.txt/}
	else
		_run=""
	fi
	# If there are no dark runs, do not plot them
	if [[ ! -e ${light/1\ sun*/dark}*.txt ]]; then
		plotcv -s ${_sample} -C ${_carbon} -c ${_comments} \
			-l ${light} -a ${area} -z -p \
			-o ${_sample}.cv${_run}.png >> $tmpfile
		plotcv -s ${_sample} -C ${_carbon} -c ${_comments} \
			-l $light -a ${area} \
			-o ${_sample}.cv_full${_run}.png
	else
		# Plot dark runs
		for dark in ${light/1\ sun*/dark}*.txt; do
			if [[ $dark == *"run"* ]]; then
				_drun=.dark_run${${${dark}/*run /}/,*.txt/}
			else
				_drun=""
			fi
			plotcv -s ${_sample} -C ${_carbon} -c ${_comments} \
				-l ${light} -d ${dark} -a ${area} -z -p \
				-o ${_sample}.cv${_run}${_drun}.png >> $tmpfile
			plotcv -s ${_sample} -C ${_carbon} -c ${_comments} \
				-l $light -d ${dark} -a ${area} \
				-o ${_sample}.cv_full${_run}${_drun}.png
		done
	fi
	printf "\n"
done

mv $tmpfile ./summary.txt
